@ on compile
@ require aether:load
@ require aether:util/rand
@ require aether:util/search
@ require aether:entities/general/entity_model
@ require aether:entities/common

define local item swet_model aether:mob_model#37

define objective capture_time

define entity swet minecraft:slime {
    
    default nbt {
        CustomName: "\"Swet\"",
        Tags: ["init","needs_id","model_target","model_holder","link_holder"],
        Size: 0,
        AbsorptionAmount: 8f
    }
    
    default passengers [
        $entity_model{ArmorItems:[{},{},{},{id:"aether:mob_model",Count:1b,tag:{CustomModelData:37,Model:{BaseModel:37}}}]}
    ]
    
    function spawn {
        summon $swet ~ ~ ~
    }
    
    function capture {
        var victim = entity<@a[distance=..5,tag=hostile_target,sort=nearest,limit=1]>
        
        set @s->entity_link = ${victim}->id
        set @s->capture_time = 1
        set @s.Size = 3
        
        effect give @s jump_boost 3 4 true
        effect give $victim resistance 3 2 true
        
        title $victim actionbar "A swet swallowed you! Kill it to escape!"
    }
    
    ticking function {
        function aether:util/search/by_id
        tag @e[tag=return,limit=1] add model_component
        var model = entity<@e[tag=model_component,limit=1]>
        set ${model}->model_offset = 0
        if entity $model if entity @s[tag=init] function {
            function aether:util/rand/generate
            set @s->op1 = 3
            scoreboard players operation RANDOM global %= @s op1
            
            if score RANDOM global matches 0 data merge entity @s {DeathLootTable:"aether:entities/blue_swet"}
            if score RANDOM global matches 1 data merge entity @s {DeathLootTable:"aether:entities/dark_swet"}
            if score RANDOM global matches 2 data merge entity @s {DeathLootTable:"aether:entities/golden_swet"}
            
            set @s->op1 = 10
            scoreboard players operation RANDOM global *= @s op1
            scoreboard players add RANDOM global 37
            set ${model}.ArmorItems[3].tag.Model.BaseModel (int) = RANDOM -> global
            tag @s remove init
        }
        if block ~ ~ ~ minecraft:water kill @s
        
        unless score @s capture_time matches 1.. if entity @a[distance=..1,tag=hostile_target] function aether:entities/swet/capture
        
        if score @s capture_time matches 1.. function {
            scoreboard players add @s capture_time 1
            
            if score @s capture_time matches 1..100 function {
                function aether:util/search/by_link
            
                var victim = entity<@e[tag=return,limit=1]>
                
                tp $victim ~ ~ ~
                
                function aether:util/search/release
                
                set ${model}->model_offset = 8
            }
            
            set @s[scores={capture_time=100}].Size = 0
            set @s[scores={capture_time=160..}]->capture_time = 0
        }
        scoreboard players remove @s[nbt={OnGround:1b},scores={jump_time=1..}] jump_time 1
        unless score @s capture_time matches 1.. scoreboard players add @s[nbt={OnGround:0b}] jump_time 1
        set @s[nbt={OnGround:1b},scores={jump_time=4..}]->jump_time = 3
        unless score @s capture_time matches 1.. function {
            if score @s jump_time matches 1..2 set ${model}->model_offset = 2
            if score @s[nbt={OnGround:1b}] jump_time matches 1..2 set ${model}->model_offset = 6
            if score @s jump_time matches 3.. set ${model}->model_offset = 4
        }
        unless entity @s[nbt={HurtTime:0s}] scoreboard players add $model model_offset 1
        
        tag $model remove model_component
        function aether:util/search/release
    }
}


define item swet_spawn_egg minecraft:slime_spawn_egg {
    default nbt {EntityTag:{id:"minecraft:area_effect_cloud",Duration:1,Radius:0f,Tags:["spawn_swet"]}}
    
    default name {"text":"Swet Spawn Egg","italic":false}
    
    function give {
        give @s $swet_spawn_egg
    }
}

define function extra {
    @ tag tick
    
    at @e[type=area_effect_cloud,tag=spawn_swet] summon $swet
    kill @e[type=area_effect_cloud,tag=spawn_swet]
    
    tag @e[type=slime,name="Swet",tag=!trident-entity.swet] add kill
}
