@ on compile
@ require aether:load
@ require aether:util/rand
@ require aether:util/search
@ require aether:entities/general/entity_model
@ require aether:entities/common

define local item sentry_model aether:mob_model#7

define entity sentry minecraft:slime {
    
    default nbt {
        CustomName: "\"Sentry\"",
        Tags: ["needs_id","model_target","model_holder","link_holder"],
        Size: 1,
        AbsorptionAmount: 6f,
        Silent:1b,
        DeathLootTable: "aether:entities/sentry"
    }
    
    default passengers [
        $entity_model{Small:1b,ArmorItems:[{},{},{},{id:"aether:mob_model",Count:1b,tag:{CustomModelData:10,Model:{BaseModel:10}}}]}
    ]
    
    function spawn {
        summon $sentry ~ ~ ~
    }
    
    ticking function {
        function aether:util/search/by_id
        tag @e[tag=return,limit=1] add model_component
        var model = entity<@e[tag=model_component,limit=1]>
        set ${model}->model_offset = 0
        
        if entity @a[tag=hostile_target,distance=..2,nbt={HurtTime:10s}] function {
            particle explosion
            playsound minecraft:entity.generic.explode master @a
            effect give @a[tag=hostile_target,distance=..3] levitation 1 34 true
            set @a[tag=hostile_target,distance=..3]->levitation_timer = 4
            at @a[tag=hostile_target,distance=..3] facing entity @s rotated ~ 0 summon $pusher ^ ^ ^.1
            at @a[tag=hostile_target,distance=..3] facing entity @s rotated ~ 0 summon $pusher ^ ^ ^.1
            at @a[tag=hostile_target,distance=..3] facing entity @s rotated ~ 0 summon $pusher ^ ^ ^.1
            tp @s ~ -512 ~
            kill $model
        }
        
        tag @s remove aggro
        if entity @a[tag=hostile_target,distance=..5] tag @s add aggro
        
        if entity @s[tag=!aggro] function {
            set ${model}->model_offset = 2
            data merge entity @s {OnGround:0b}
            effect give @s slowness 1 255 true
        }
        if entity @s[tag=aggro] function {
            if entity @s[tag=wasOnGround,nbt={OnGround:0b}] playsound aether:aemob.sentry.jump hostile @a
            effect clear @s slowness
        }
        
        unless entity @s[nbt={HurtTime:0s}] function {
            if entity @s[nbt={HurtTime:10s}] playsound aether:aemob.sentry.jump hostile @a
            set ${model}->model_offset = 1
            set @s->hurt_time = 60
        }
        
        tag @s remove wasOnGround
        tag @s[nbt={OnGround:1b}] add wasOnGround
        
        tag $model remove model_component
        function aether:util/search/release
    }
}


define item sentry_spawn_egg minecraft:vindicator_spawn_egg {
    default nbt {EntityTag:{id:"minecraft:area_effect_cloud",Duration:1,Radius:0f,Tags:["spawn_sentry"]}}
    
    default name {"text":"Sentry Spawn Egg","italic":false}
    
    function give {
        give @s $sentry_spawn_egg
    }
}

define function extra {
    @ tag tick
    
    at @e[type=area_effect_cloud,tag=spawn_sentry] summon $sentry
    kill @e[type=area_effect_cloud,tag=spawn_sentry]
    
    tag @e[type=slime,name="Sentry",tag=!trident-entity.sentry] add kill
}
